apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "fr.xgouchet.gitsp"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    /** Stores the signing configs information */
    signingConfigs {
        debug {
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/debug.keystore")
        }

        release {
            Properties props = new Properties()
            props.load(new FileInputStream("${projectDir.parentFile.parentFile.absolutePath}/.publish/keystores.properties"))
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/xgouchet.keystore")

            storePassword props.releaseStorePassword
            keyAlias props.releaseKeyAlias
            keyPassword props.releaseKeyPassword
        }
    }

    /** The build types */
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // ANDROID SUPPORT LIBRARIES
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.android.support:design:23.3.0'

    // ARCHITECTURE LIBRARIES
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:8.0.0'
//    compile 'com.google.android.agera:agera:1.0.0-rc2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.4'

    // JGit library
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.3.0.201604071810-r'

    // APT
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    // UNIT TESTS
    testCompile 'com.android.support:support-annotations:23.3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.6.1'
    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
