apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "fr.xgouchet.gitstorageprovider"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    /** Stores the signing configs information */
    signingConfigs {
        debug {
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/debug.keystore")
        }

        release {
            Properties props = new Properties()
            props.load(new FileInputStream("${projectDir.parentFile.parentFile.absolutePath}/.publish/keystores.properties"))
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/xgouchet.keystore")

            storePassword props.releaseStorePassword
            keyAlias props.releaseKeyAlias
            keyPassword props.releaseKeyPassword
        }
    }

    /** The build types */
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        debugCert {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Android App Compat (Material)
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.android.support:support-annotations:22.0.0'

    // ARCHITECTURE LIBRARIES
    compile 'com.squareup:otto:1.3.5'

    // JGit library
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.+'

    // UNIT TESTS
    testCompile 'com.android.support:support-annotations:22.0.0'
    testCompile('junit:junit:4.12')
    testCompile 'org.assertj:assertj-core:1.6.1'
    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
